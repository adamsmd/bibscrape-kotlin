// NOTE: Groups with comment headers are sorted alphabetically by group name (TODO)

plugins {
  kotlin("jvm") // version determined by buildSrc/build.gradle.kts
  application

  // Code Analysis
  id("io.gitlab.arturbosch.detekt").version("1.22.0") // Tasks: detekt

  // Code Coverage
  id("jacoco") // Tasks: jacocoTestReport
  id("org.jetbrains.kotlinx.kover") version "0.6.1" // Tasks: koverMergedHtmlReport

  // Code Style
  // id("com.ncorti.ktfmt.gradle") version "0.11.0" // Tasks: ktfmtCheck (omitted because issues errors not warnings)
  // id("org.cqfn.diktat.diktat-gradle-plugin") version "1.0.3" // Tasks: diktatCheck
  id("org.jlleitschuh.gradle.ktlint") version "10.2.1" // Tasks: ktlintCheck (requires disabling diktat)

  // Dependency Licenses
  id("com.github.jk1.dependency-license-report") version "2.1" // Tasks: generateLicenseReport

  // Dependency Versions
  id("com.github.ben-manes.versions") version "0.44.0" // Tasks: dependencyUpdates

  // Documentation
  id("org.jetbrains.dokka") version "1.7.20" // Tasks: dokka{Gfm,Html,Javadoc,Jekyll}

  // Typesafe config (TODO)
}

repositories {
  mavenCentral()
  maven { url = uri("https://dev.bibsonomy.org/maven2/") }
}

dependencies {
  // BibTeX
  // $ wget --recursive --timestamping --reject=html,dvi,ltx,pdf,ps.gz,ps.xz,sok,twx,db --reject='bib*' --reject='filehdr-*' --reject-regex='.*/(RCS|idx|toc)/.*' ftp://ftp.math.utah.edu/pub/tex/bib/
  implementation("org.bibsonomy:bibsonomy-bibtex-parser:3.9.4")
  implementation("org.jbibtex:jbibtex:1.0.20")

  // Command-line argument parsing
  implementation("com.github.ajalt.clikt:clikt:3.5.1")

  // ISBN
  implementation("com.github.ladutsko:isbn-core:1.1.0")

  // Logging
  implementation("ch.qos.logback:logback-classic:1.2.6")
  implementation("io.github.microutils:kotlin-logging-jvm:2.1.21")

  // Testing
  testImplementation(kotlin("test:1.8.0"))

  // WebDriver
  implementation("org.seleniumhq.selenium:selenium-java:4.7.2")
}

// ////////////////////////////////////////////////////////////////
// Main
application {
  mainClass.set("org.michaeldadams.bibscrape.MainKt")
}

// ////////////////////////////////////////////////////////////////
// Testing
tasks.withType<Test> {
  useJUnitPlatform()

  this.testLogging {
    this.showStandardStreams = true
  }
}

// ////////////////////////////////////////////////////////////////
// Version Information
// Create a new version with: git tag -a v2023.01.01
task("version") {
  doLast {
    println(project.version)
  }
}

org.eclipse.jgit.api.Git.open(project.rootDir).use { git ->
  val describe = git.describe().apply { setMatch("v*") }.call()
  val isClean = git.status().call().isClean
  project.version =
    describe
      .removePrefix("v")
      .plus(if (isClean) { "" } else { "-dirty" })
}

fun generateSrc(fileName: String, code: String) {
  val generatedSrcDir = File(buildDir, "generated/main/kotlin")
  generatedSrcDir.mkdirs()
  kotlin.sourceSets["main"].kotlin.srcDir(generatedSrcDir)
  val file = File(generatedSrcDir, fileName)
  file.writeText(code)
}

val generateBuildInfo by tasks.registering {
  doLast {
    // TODO: avoid running when unchanged

    val code = """
      // Do not edit this file by hand.  It is generated by `gradle`.
      package org.michaeldadams.bibscrape

      /** This object was generated by `gradle`. */
      object BuildInformation {
        val version: String = "${project.version}"
      }
      """.trimIndent().trimStart()

    generateSrc("BuildInformation.kt", code)
  }
}

// ////////////////////////////////////////////////////////////////
// Code Analysis
detekt {
  ignoreFailures = true
  buildUponDefaultConfig = true
  allRules = true
}

// ////////////////////////////////////////////////////////////////
// Code Formatting

// TODO:
// indentWithSpaces(2) // or spaces. Takes an integer argument if you don't like 4
// trimTrailingWhitespace()
// endWithNewline()

// diktat {
//   ignoreFailures = true
// }

ktlint {
  verbose.set(true)
  ignoreFailures.set(true)
  enableExperimentalRules.set(true)
  disabledRules.set(
    setOf(
      "experimental:argument-list-wrapping",
      "no-wildcard-imports",
    )
  )
}

// ////////////////////////////////////////////////////////////////
// Documentation
tasks.withType<org.jetbrains.dokka.gradle.DokkaTask>().configureEach {
  dokkaSourceSets {
    named("main") {
      includes.from("Module.md")
    }
  }
}

// ////////////////////////////////////////////////////////////////
// Task Dependencies

// For why we have to fully qualify KotlinCompile see:
// https://stackoverflow.com/questions/55456176/unresolved-reference-compilekotlin-in-build-gradle-kts
tasks.withType<org.jetbrains.kotlin.gradle.tasks.KotlinCompile> {
  // Avoid the warning: 'compileJava' task (current target is 11) and
  // 'compileKotlin' task (current target is 1.8) jvm target compatibility should
  // be set to the same Java version.
  kotlinOptions { jvmTarget = project.java.targetCompatibility.toString() }

  dependsOn(generateBuildInfo)
}
